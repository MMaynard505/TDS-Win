. ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
. บ  TJONINV3      Print TDS invoices                                          บ
. บ  V.M=3.0                                                                   บ
. บ                                                                            บ
. ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
.
. ษออออออออออัอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
. บ  Date    ณ                 Modification                                    บ
. วฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤถ
. บ 01/21/98 ณ V.M=2.A Added Billing Month line to invoice import file         บ
. บ 03/29/98 ณ Added code to prompt for checking the year when MM=01           บ
. บ 09/27/98 ณ Sound Ford will not be billed for 3 bureaus (Only 1)  MM        บ
. บ 01/21/99 ณ Ver 3.0 Created from Ver 2.A (For PLBWin)                       บ
. บ          ณ Some tweaking with SPLFiles was required...MM                   บ
. บ          ณ                                                                 บ
. บ          ณ                                                                 บ
. ศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ


         INCLUDE   COMMON.INC

Version  INIT      "TDSInv3 V.M=3.0"
FF       EQU       12
P30      DIM       29
P22      DIM       24
P18      DIM       18
P52      DIM       51
P5       DIM       7
P40      DIM       42
P62 INIT "                                                                " 61
P58 INIT "                                                           "      57
BLANKS   DIM       80
Reply    INIT      "N"     FORMS ALIGNMENT REPLY
Drive    INIT      "$TDSDATA\"                    ;; 1/22/99 Data Location
INTSW    INIT      " "     Y=Interrupt in progress
MM       DIM       2
DD       DIM       2
YY       DIM       2
YYYY     DIM       4         /* Year with the century      1/10/98
WORK2    DIM       2
YearWork FORM      2
YearTest FORM      "75"      /* If year is < 75 then Cent=20 else Cent=19
Century  DIM       2
Cent19   INIT      "19"
Cent20   INIT      "20"
LInvDate DIM       8         /* 1/10/98 Last Invoice Date for customer

ACCUMST  FORM      6.3       /* Accumulated Total Amount Taxable
SALESTAX FORM      6.2       /* Computed Sales Tax
DSTAX    DIM       11        /* Used to display the Total Sales Tax computed

CustMast IFILE     VARIABLE=1024,NODUP,WEOF

TimeFile FILE

CtrlFile IFILE

InvDist  IFILE

IDKey    DIM       4         ;; 1/11/98  Invoice Distribution File Key
IDNbr    FORM      2         ;; 1/11/98  Distribution number for this invoice

. LAST BILL MO MM YY xxxx yyyy $$$$$.$$

LastBMO  INIT      "LAST BILL MO"
LastBMM  FORM      2
LastBYY  FORM      2
LastSIN  FORM      4
LastEIN  FORM      4
LastAmt  FORM      5.2
MMWork   DIM       2
YYWork   DIM       2
BillWork DIM       5

TJONInvF IFILE     VARIABLE=1024,NODUP,WEOF ;; Inv history file (Indexed 1-7)

Invoices IFILE     VARIABLE=1024,NODUP,WEOF ;; Secondary TJONINVF index INVNUM

SF       FILE                             ;; Used to check for spool file
SPLFILE  PFILE                            ;; Spool file

SFName   DIM       18                     ;;      "TJONINV3.PRT"
SplName  DIM       50                     ;;      Drive + SFName

. ***************************************************************************
. The following variables, constants, and files are for the new import
. process. This will allow invoices to be generated by this program and then
. imported into Peachtree First Accounting by using the standard PeachTree
. import process for sales records.

PTIFName INIT      "$TDSDATA\TDSSALES.CSV"
PTIF     FILE
Backward FORM      "-4"                   ;; Sequentially read file backwards
Append   FORM      "-3"                   ;; Append new records to EOF
Forward  FORM      "-2"                   ;; Sequentially read file forward
PTIFSeq  FORM      "-2"                   ;; Stanard sequential file processing

PTHeader INIT      "Customer ID,Job ID,Date,G/L Account,":
                   "Quantity,Item ID,Description,Unit Price,Amount,":
                   "Invoice ##,":
                   "Accounts Receivable Account,":
                   "Number of Distributions,Tax Type"

PTCustID DIM       3            ;; Customer ID
PTJobID  DIM       10           ;; Job ID
PTDate   DIM       8            ;; Date
PTGLAcct DIM       5            ;; G/L Account
PTQuant  DIM       6            ;; Quantity                  999.99
PTItemID DIM       10           ;; Item ID
PTDescrp DIM       60           ;; Item Description
PTUnitPr DIM       9            ;; Unit Price                999999.99
PTAmount DIM       9            ;; Amount                    999999.99
PTInvNbr DIM       4            ;; Invoice #                 9999
PTARAcct DIM       5            ;; Accounts Receivable Account
PTDistr  DIM       2            ;; Number of Distributions   99
PTTaxTyp INIT      " "          ;; Tax Type

WkQuant  FORM      3.2          ;; Numeric work area for PTQuant
WkUnitPr FORM      6.2          ;; Numeric work area for PTUnitPr
WkAmount FORM      6.2          ;; Numeric work area for PTAmount
WkInvNbr FORM      4            ;; Numeric work area for PTInvNbr
WkDistr  FORM      2            ;; Numeric work area for PTDistr

NMTax    INIT      "2"          ;; New Mexico State Gross Receipts Tax
Exempt   INIT      "1"          ;; Exempt from Sales Taxes

Comma    INIT      ","          ;; CSV seperator character (comma)

PTImport VARLIST   PTCustID:    ;; Customer ID
                   Comma:
                   PTJobID:     ;; Job ID
                   Comma:
                   PTDate:      ;; Date
                   Comma:
                   PTGLAcct:    ;; G/L Account
                   Comma:
                   PTQuant:     ;; Quantity
                   Comma:
                   PTItemID:    ;; Item ID
                   Comma:
                   PTDescrp:    ;; Item Description
                   Comma:
                   PTUnitPr:    ;; Unit Price
                   Comma:
                   PTAmount:    ;; Amount
                   Comma:
                   PTInvNbr:    ;; Invoice #
                   Comma:
                   PTARAcct:    ;; Accounts Receivable Account
                   Comma:
                   PTDistr:     ;; Number of Distributions
                   Comma:
                   PTTaxTyp     ;; Tax Type

DCAcct   INIT      "40000"      ;; Diskette Conversion G/L Account
SCAcct   INIT      "40800"      ;; Shipping Charge     G/L Account
ARAcct   INIT      "11000"      ;; A/R Account Number
DC       INIT      "DC"         ;; Diskette Conversion - CBI
DCFC     INIT      "DCFC"       ;; Diskette Conversion (Florida Credit)
DCSF     INIT      "DCSF"       ;; Diskette Conversion (Sound Ford)
DE       INIT      "DE"         ;; Diskette Conversion - Experian
DT       INIT      "DT"         ;; Diskette Conversion - TRW
DU       INIT      "DU"         ;; Diskette Conversion - TU
DS       INIT      "SPCL3"      ;; Diskette Conversion - Special for all 3 bur.
SHIPCHG  INIT      "SHIPCHG"    ;; Shipping and handling charges
SHIPCHGC INIT      "SHIPCHGC"   ;; Shipping & handling chg CBI
SHIPCHGE INIT      "SHIPCHGE"   ;; Shipping & handling chg Exprn
SHIPCHGT INIT      "SHIPCHGT"   ;; Shipping & handling chg TRW
SHIPCHGU INIT      "SHIPCHGU"   ;; Shipping & handling chg TU
SCd      INIT      "Shipping and handling charges."
SCdC     INIT      "Shipping and handling charges for tapes sent to CBI."
SCdT     INIT      "Shipping and handling charges for tapes sent to TRW."
SCdU     INIT      "Shipping and handling charges for tapes sent to TU."
SCdE     INIT      "Shipping and handling charges for tapes sent to Experian."
DCdC  INIT "Diskette to tape conversions for credit reporting to CBI."
DCdT  INIT "Diskette to tape conversions for credit reporting to TRW."
DCdU  INIT "Diskette to tape conversions for credit reporting to TU."
DCdE  INIT "Diskette to tape conversions for credit reporting to Experian."
DCdF  INIT "Auto Credit of Florida charge for credit reporting to TU."
DCdSF INIT "Special pricing for all 3 credit bureaus for Sound Ford."
DCdS  INIT "Special pricing for all 3 credit bureaus.                "
.
.
.
. ***************************************************************************


.
.   Note....  See also INVRECDS.INC
.
         INCLUDE   TJONINVF.INC                   /*  3/17/88...JMM
WDMFL    IFILE                                    /* 12/01/86...JMM
WDMKEY   DIM       6

         INCLUDE   TJONCMDS.INC

         INCLUDE   INVLNEDS.INC

         INCLUDE   TIMERCDS.INC

.    This table needs to be changed once a year, & program recompiled...
         INCLUDE   MONTHTBL.INC

         INCLUDE   TRDSWDM.INC                     ;; 12/01/86...JMM

KEY      DIM       3
CTRLKEY  DIM       12
SEQ      FORM      "-2"
SEQ1     FORM      "-2"
SEQ2     FORM      "-2"
INVTOTAL FORM      7.2
SAVECUST DIM       3
EOJSW    DIM       1
FIRSTSW  DIM       1
PSUMFT   INIT      "Y"
PSUNITS  FORM      6                     /* Totals for Summary report
PSDISTS  FORM      3
PSTOTAL  FORM      7.2
PSLINES  FORM      "99"
PSPAGE   FORM      " 0"
PSITOTAL DIM       13                   /* Invoice total for prt summary rpt
TOF      EQU       014     /* Decimal 12 = Printer top of form
CR       EQU       015     /*              Printer Carriage Return
LF       EQU       012     /*              Printer Line Feed
SEPLINE  INIT      "----------------------------------------"  40 DASHES
.
MONTH    DIM       2
DAY      DIM       2
YEAR     DIM       2
ClockYY  DIM       2             ;; Last 2 digits from year (Current OS date)
.
D1       DIM       1
INVSW    DIM       1
SAVEINUM FORM      4
SAVEWO   FORM      4             /* Save area for Work Order number   3/31/88
SAVEIWO  INIT      "????"        /* Save area for Work Order number   3/31/88
INVNUMS  DIM       4                 SAVE AREA FOR INVOICE NBR
LINE1    DIM       30                         12/9...JMM
DIMWRK9  DIM       9
DIMWRK10 DIM       10
FILLER   DIM       12
NEXTINV  FORM      4
BILLMNTH DIM       15
INDEX    FORM      2
WORKYEAR DIM       2
NEWYEAR  FORM      2
IFSCUST  DIM       3
IFSINV   DIM       4
IFKEYSV  DIM       7
EAMTMASK INIT      "$$$,$$$.99-"
TAMTMASK INIT      "$,$$$,$$$.99-"
DEXTCOST DIM       11                   /* USED TO DISPLAY EXT COST
DITOTAL  DIM       13                   /* USED TO DISPLAY THE INVOICE TOTAL
COSTWORK FORM      6.2                  /* Extended cost must be moved here!
DUEDATE  INIT      "MM/DD/YY"           /* INVOICE DUE DATE
IPDate   INIT      "MM/DD/YY"           /* Invoice Print Date      3/10/94
BILLMO   INIT      "00"
BMonth   DIM       3                    ;; 3 Character descriptive month (JAN)
GrandTot FORM      7.2                  /* GRAND TOTAL OF INVOICES PRINTED
NBRINVS  FORM      "  0"                /* NUMBER OF INVOICES PRINTED
RecIn    DIM       127
.
. INITIALIZE FILLERS TO SPACES...JMM
.
.
TJONINV3 RESET     BLANKS TO 80
         LENSET    BLANKS
         RESET     BLANKS
.
         CLOCK     DATE TO TODAY                   /* 3/10/94
         UNPACK    TODAY TO MONTH,D1,DAY,D1,YEAR   /* 1/10/98
         MOVE      Year TO YearWork
         MOVE      Year TO ClockYY                 ;; 3/29/98
         COMPARE   YearTest TO YearWork
         GOTO      Year19xx IF NOT LESS
         MOVE      Cent20 TO Century
         GOTO      CentOK
Year19xx
         MOVE      Cent19 TO Century
CentOK
         PACK      YYYY FROM Century, Year         /* 1/10/98...JMM
         TRAP      INT IF INTERRUPT
         MOVE      BLANKS TO IFFILLER              /* 3/17/88...JMM
         MOVE      BLANKS TO P30
         MOVE      BLANKS TO P22
         MOVE      BLANKS TO P52
         MOVE      BLANKS TO P40
         MOVE      BLANKS TO P5
         MOVE      BLANKS TO P62
         MOVE      BLANKS TO P18
.
         MOVE      "0.0" TO GRANDTOT          /* CLEAR GRAND TOTAL
.
         TRAP      NOIF IF IO
         OPEN      TJONINVF,"TJONINVF"
         TRAPCLR   IO
         TRAP      NOIF2 IF IO
         OPEN      INVOICES,"INVOICES"
         TRAPCLR   IO

         TRAP      INVMISS IF IO
         OPEN      TIMEFILE,"TIMEFILE"
         TRAPCLR   IO

         TRAP      InvDMiss IF IO
         OPEN      InvDist,"INVDIST"
         TRAPCLR   IO

         TRAP      ARMISS IF IO
         OPEN      CUSTMAST,"CUSTMAST"
         TRAPCLR   IO

         TRAP      CTRLMISS IF IO
         OPEN      CTRLFILE,"CTRLFILE"
         TRAPCLR   IO

         MOVE      "NEXT INVOICE" TO CTRLKEY
         MOVE      "N" TO EOJSW
         MOVE      "999" TO SaveCust
         MOVE      "Y" TO FIRSTSW
         MOVE      "Y" TO INVSW
         UNPACK    TODAY TO MM, REPLY, DD, REPLY, YY
         TRAP      NOCTLREC IF RANGE
         READ      CTRLFILE,CTRLKEY;FILLER,NEXTINV
         GOTO      NOCTLREC IF OVER
         TRAPCLR   RANGE
         CLOSE     CTRLFILE

         MOVE      NEXTINV TO SAVEINUM
         MOVE      NextInv TO LastSIN       ;; Save starting Invoice Nbr

         DISPLAY   *COLOR 23,*ES:    INITIAL CLEAR OF THE SCREEN
                   *P01:01,Version:
                   *COLOR 19:
                   *P25:04,"    TDS Conversions     ":
                   *P25:06," Invoice Print Program  ":
                   *COLOR 23:
                   *P56:10,"Starting Invoice....",*COLOR 19,NEXTINV:
                   *COLOR 23:
                   *P56:11,"Ending   Invoice....",*COLOR 19,"xxxx":
                   *COLOR 23;

.................................................................
. Parsing of command line added 7/22/95
....................................................................

         GOTO      ParseCmd
CmdLine  INIT      "N"
Argument DIM       30(5)              ;; 5 parameters - 30 bytes each
ArgCount FORM      2
Range    INIT      "azAZ09..$$"
PMax     FORM      "5"                ;; Maximum number of parameters
.
ParseCmd
         CMATCH    " " TO S$CMDLIN
         GOTO      Parsexit IF EOS               /* This tests for null input
         BUMP      S$CMDLIN
         GOTO      Parsexit IF EOS               /* This tests for null input
Parse1
         COMPARE   ARGCOUNT TO PMax
         GOTO      Parsexit IF EQUAL
         ADD       "1" TO ARGCOUNT
         PARSE     S$CMDLIN,ARGUMENT(ARGCOUNT),RANGE,UPPERCASE
         GOTO      Parse2 IF NOT EQUAL
         GOTO      Parse3 IF LESS
         GOTO      Parse1
Parse2
         SUB       "1",ARGCOUNT
         BUMP      S$CMDLIN
         GOTO      Parse1 IF NOT EOS
Parse3
         MOVE      ARGUMENT(1) TO BillMo      ;; 1st parm is billing month
         MOVE      ARGUMENT(2) TO Month       ;; 2nd parm is inv month
         MOVE      ARGUMENT(3) TO Day         ;; 3rd parm is inv month
         MOVE      ARGUMENT(4) TO Year        ;; 4th parm is inv month

         MOVE      "Y" TO CmdLine          ;; Command line options in effect

Parsexit
. No-Op Keyin      *N,"Exit parsecmd. Arg1=",*DV,Argument(1):
. No-Op            ", Arg2=",*DV,Argument(2):
. No-Op            *N,"Arg3=",*DV,Argument(3):
. No-Op            ", Arg4=",*DV,Argument(4):
. No-Op            *N,"Arg5=",*DV,Argument(5):
. No-Op            Reply;
.
....................................................................
KEYBM
         MATCH     "Y" TO CmdLine
         GOTO      NoKeyBM IF EQUAL        ;; Do we need to get parms?
         KEYIN     *B,*P56:10,"Starting Invoice....",*DV,NEXTINV:
                   *P56:11,"Ending   Invoice....xxxx":
                   *P01:08,"Enter Billing Month    [MM] ",BILLMO:
                   *P01:10,"Enter Invoice Due Date [MM/DD/YY]   ";
         CMATCH    "*" TO BILLMO
         GOTO      KEYBM IF EOS
         GOTO      SHUTDN IF EQUAL          /* ALLOW TERMINATION
NoKeyBM
         TYPE      BILLMO
         GOTO      KEYBM IF NOT EQUAL
         MATCH     "99" TO BILLMO
         GOTO      SHUTDN IF EQUAL          /* Allow termination
         MATCH     "01" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "02" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "03" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "04" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "05" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "06" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "07" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "08" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "09" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "10" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "11" TO BILLMO
         GOTO      KEYMONTH IF EQUAL        /* Verify month is OK
         MATCH     "12" TO BILLMO
         GOTO      KEYBM IF NOT EQUAL       /* Verify month is OK
KEYMONTH
         MOVE      BILLMO TO INDEX
         LOAD      BILLMNTH FROM INDEX OF M1,M2,M3,M4,M5,M6,M7:
                                          M8,M9,M10,M11,M12
         LOAD      BMonth   FROM INDEX OF MMM1,MMM2,MMM3:
                                          MMM4,MMM5,MMM6:
                                          MMM7,MMM8,MMM9:
                                          MMM10,MMM11,MMM12
         MATCH     "01" TO M1             ;; 3/29/98
         GOTO      CkCL IF NOT EQUAL      ;; 3/29/98
         MATCH     MTYear TO ClockYY      ;; 3/29/98
         GOTO      CkCL IF EQUAL          ;; 3/29/98

. The following section was added 3/29/98
         KEYIN   *B,*P1:09,"The invoicing will be for the month of ":
                 *DV,BILLMNTH,"/",*DV,MTYear:
                 *P1:11,"The current system Year is ",*DV,Century,*DV,ClockYY:
                 *P1:12,"The MONTHTBL file has Year=",*DV,MTYear:
                 *P1:13,"Is this correct? [Y/N] ",*CL,*-,Reply;
         CMATCH    "Y" TO Reply
         GOTO      CkCL IF EQUAL
         DISPLAY   *P1:14,*EL,"Change MONTHTBL.INC and recompile TJONINV3.DBS";
         STOP
CkCL
         CMATCH    "Y" TO CmdLine
         GOTO      NoKeyMo IF EQUAL        ;; Do we need to get parms?
         KEYIN     *+,MONTH,"/";
         CMATCH    " " TO MONTH
         GOTO      KEYMONTH IF EOS
KeyDay
         KEYIN     *+,DAY,"/";
         CMATCH    " " TO DAY
         GOTO      KEYDAY IF EOS
KeyYear
         KEYIN     *+,YEAR:
                   *N
         CMATCH    " " TO YEAR
         GOTO      KEYYEAR IF EOS
         KEYIN     *P1:09,"The invoicing will be for the month of ":
                   *DV,BILLMNTH:
                   *P1:11,"The invoice date [MM/DD/YY] will be ":
                   *DV,BILLMO,"/",*DV,DAY,"/",*DV,YEAR:
                   *P1:12,"Is this correct? [Y/N] ",*CL,*-,REPLY;
         CMATCH    "Y" TO REPLY
         GOTO      PACKDD IF EQUAL
         CMATCH    "y" TO REPLY
         GOTO      KEYBM IF NOT EQUAL
NoKeyMo
         CMATCH    " " TO YEAR
         GOTO      KeyBM    IF EOS        ;; Go all the way back if bad parms
PackDD
         MOVE      "/" TO REPLY
         PACK      DUEDATE FROM MONTH,REPLY,DAY,REPLY,YEAR
         UNPACK    TODAY TO MONTH,D1,DAY,D1,YEAR          ;; 3/10/94
         PACK      IPDate FROM MONTH,REPLY,DAY,REPLY,YEAR ;; 3/10/94
         PACK      LInvDate FROM Month, Day, YYYY         ;; 1/10/98
         DISPLAY   *P1:20,*COLOR 124,*EL:
                   "Billing Month=",BillMo:
                   ", Today=",Today:
                   ", DueDate=",DueDate:
                   ", IPDate=",IPDate;
.
.
GetHC
         CALL      CKSF             ;; 7/22/95 Changed to call this routine
         CALL      CkPTIF           ;; 1/10/98 Call the Checking of Import File
         CALL      GetDist          ;; Routine to get distribution

ReadTime
         READ      TIMEFILE,SEQ;TimeRcd
         GOTO      EOJ IF OVER

CkCust
         MATCH     "000" TO ICust
         GOTO      ChkINum IF NOT EQUAL
.
.   Null Records are bypassed
.
         MOVE      "000" TO SaveCust
         GOTO      ReadTime

ChkINum
         MOVE      IINVNUM TO INVNUMS    SAVE INV THAT CAME FROM TJONINV2
         MOVE      SAVEINUM TO IINVNUM
         MOVE      ICUST TO KEY
         MOVE      "0000" TO SAVEWO             /* 3/31/88
         MOVE      IWO TO SAVEWO                /* 3/31/88
         COMPARE   "0" TO SAVEWO                /* 3/31/88
         GOTO      WOBP2 IF EQUAL               /* 3/31/88
         COMPARE   "9999" TO SAVEWO             /* 3/31/88
         GOTO      WOBP2 IF EQUAL               /* 3/31/88
         MOVE      SAVEWO TO SAVEIWO
WOBP2
         CMATCH    "Y" TO FirstSw            /* Initial program entry?
         GOTO      ChkCust IF NOT EQUAL      /* Then check for cust change
         CMOVE     "N" TO FirstSw            /* Reset first time flag
         GOTO      ReadAR                    /* And begin processing
ChkCust
         MATCH     ICust TO SaveCust         /* Is this the same customer?
         GOTO      TotalInv IF NOT EQUAL     /* Go total the invoice...JMM
         MATCH     "0000" TO INVNUMS
         GOTO      PrintDtl IF EQUAL
         GOTO      TotalInv                  /* Go total the invoice

ReadAR
         READ      CUSTMAST,KEY;CustRcd
         GOTO      CustNF IF OVER
         TRAPCLR   RANGE
         MOVE      CUSTATTN TO LINE1          USE CUST FILE FOR LINE1 12/8
         MOVE      LInvDate TO CustLInD       /* 1/10/98
         UPDATE    CUSTMAST;CustRcd           /* 1/10/98
PrintInv
         CALL      BMLine                     ;; 1/21/98 BM Line for PTree
         PRINT     *N,*N,*N,*N:                      /* Remove *F...JMM
                   *N,*N,*N:                         /* 7/27/87...JMM
                   *60,"Invoice ":
                   *70,SAVEINUM:
                   *N,*60,"Customer   ",CustNum:      9/12/85...JMM
                   *N,*53,"Invoice date ",IPDate:     3/10/94...JMM
                   *8,LINE1:
                   *N:
                   *8,CUSTNAME:
                   *N:
                   *8,CUSTADDR:
                   *N:
                   *8,CUSTCITY," ",CUSTSTE,"  ",CUSTZIP:
                   *N,*N,*N,*N:
                   *8,INVLINE1:
                   " ",BILLMNTH:
                   *N,*N,*N,*N:
                   *8,"Quantity":
                   *23,"---------Description--------":
                   *58,"Rate":
                   *69,"Amount"

PrintDtl
         MOVE      IQUAN TO DIMWRK10
         BUMP      DIMWRK10 BY 7
         MATCH     ".00" TO DIMWRK10
         GOTO      PrintQty IF NOT EQUAL
         BUMP      DIMWRK10 BY -1
         LENSET    DIMWRK10
         RESET     DIMWRK10

PrintQty
         PRINT     *N,*6,DIMWRK10:
                   *17,ITYPE:
                   *24,IDESC1;
         MATCH     "                            " TO IDESC2
         GOTO      NOSPACE IF EQUAL
         CMATCH    " " TO IDESC2
         GOTO      NOSPACE IF EOS
         PRINT     *N:
                   *24,IDESC2;

NoSpace  MOVE      IUPRICE TO DIMWRK9
         BUMP      DIMWRK9 BY 7
         MATCH     "00" TO DIMWRK9
         GOTO      PRNTUNIT IF NOT EQUAL
         BUMP      DIMWRK9 BY -1
         LENSET    DIMWRK9
         RESET     DIMWRK9
         MATCH     "    .00" TO DIMWRK9                  12/9
         GOTO      PRNTUNIT IF NOT EQUAL
         MOVE      "       " TO DIMWRK9                  12/9

PrntUnit
         MOVE      EAMTMASK TO DEXTCOST
         MOVE      "000000.00" TO CostWork          /* 9/12/85...JMM
         MOVE      IEXTCOST    TO CostWork          /* 9/12/85...JMM
         EDIT      COSTWORK    TO DEXTCOST          /* 9/12/85...JMM
         PRINT     *55,DIMWRK9:
                   *66,DEXTCOST
         ADD       IEXTCOST TO INVTOTAL

.
.  Sales Tax Processing                (3/31/88)
.

................ Debug Code ...................
. No-Op Keyin      *N,"Enter Sales Tax Processing",*DV,PTTaxTyp:
.                  Reply;
................ Debug Code ...................

         CMATCH    "T" TO ITAXFLAG            /* Is this a taxable amount?
         GOTO      NoSTax2 IF NOT EQUAL       /* No,  skip
         ADD       IEXTCOST TO ACCUMST        /* Accumulate Taxable Total
         MOVE      NMTax TO PTTaxTyp          ;; Taxable line item

NoSTax2

................ Debug Code ...................
. No-Op Keyin      *N,"Exit  Sales Tax Processing",*DV,PTTaxTyp:
.                  Reply;
................ Debug Code ...................


.  Build and write the Peachtree Import File here         Jan '98

         MOVE      ARAcct   TO PTARAcct       ;; A/R Account Number
. 1/12/98 If Invoice number is null, then PT will print the invoice
. No-Op  MOVE      SaveINum TO WkInvNbr       ;; Invoice Number
. No-Op  MOVE      WkInvNbr TO PTInvNbr       ;; Back to output area
. No-Op  REPLACE   " 0"     IN PTInvNbr       ;; Zero fill
         CLEAR     PTInvNbr                   ;; 1/12/98 Null field

         MOVE      CustNum  TO PTCustID       ;; Customer ID
         MOVE      IPDate   TO PTDate         ;; Date
         MOVE      CostWork TO WkAmount       ;; Amount (Total)
         MOVE      WkAmount TO PTAmount       ;; Back to output area
         REPLACE   " 0"     IN PTAmount       ;; Zero fill
         CMOVE     "-"      TO PTAmount       ;;   Debit

         PACK      PTJobID FROM BMonth, MTYear, "DC"
         MOVE      IUPrice  TO WkUnitPr       ;; Unit price
         MOVE      WkUnitPr TO PTUnitPr       ;; Unit price
         REPLACE   " 0"     IN PTUnitPr       ;; Zero fill
         MOVE      IQuan    TO WkQuant        ;; Quantity
         MOVE      WkQuant  TO PTQuant        ;; Quantity
         REPLACE   " 0"     IN PTQuant        ;; Zero fill
         MOVE      SCAcct   TO PTGLAcct       ;; Default to Shipping Charge

         MATCH     "Shipping & Handling charges." TO IDesc1
         GOTO      GetSC IF EQUAL             ;; Must be a SC
         MOVE      DCAcct TO PTGLAcct         ;; GL Code for DC

         MATCH     "Diskette to tape conversions" TO IDesc1
         GOTO      GetDC IF EQUAL             ;; Must be a DC

         MATCH     "Special pricing for reports " TO IDesc1
         GOTO      ItemSpc3 IF EQUAL          ;; Special pricing
         MATCH     "sent to all 3 credit bureaus" TO IDesc2
         GOTO      ItemSpc3 IF EQUAL          ;; Special pricing
         MATCH     "AutoCredit of Florida charge" TO IDesc1
         GOTO      ItemACF1 IF EQUAL
         MATCH     "Sound Ford conversion charge" TO IDesc1
         GOTO      ItemSF1 IF EQUAL

         GOTO      PTIFOut                    ;; Some other kind of charge

GetSC
         MATCH     "(Tapes sent to CBI-Atlanta) " TO IDesc2
         GOTO      ItemCBI2 IF EQUAL
         MATCH     "(Tapes sent to TRW-Allen,TX)" TO IDesc2
         GOTO      ItemTRW2 IF EQUAL
         MATCH     "(Tapes sent to TU-Chicago)  " TO IDesc2
         GOTO      ItemTU2  IF EQUAL
         MOVE      "SHIPCHG"  TO PTItemID
         MOVE      SCd TO PTDescrp
         GOTO      PTIFOut
ItemCBI2
         MOVE      "SHIPCHGC" TO PTItemID
         MOVE      SCdC TO PTDescrp
         GOTO      PTIFOut
ItemTRW2
         MOVE      "SHIPCHGT" TO PTItemID
         MOVE      SCdT TO PTDescrp
         GOTO      PTIFOut
ItemTU2
         MOVE      "SHIPCHGU" TO PTItemID
         MOVE      SCdU TO PTDescrp
         GOTO      PTIFOut

GetDC
         MATCH     "for credit reporting to CBI." TO IDesc2
         GOTO      ItemCBI1 IF EQUAL
         MATCH     "for credit reporting to TRW." TO IDesc2
         GOTO      ItemTRW1 IF EQUAL
         MATCH     "for credit reporting to TU. " TO IDesc2
         GOTO      ItemTU1 IF EQUAL
         MATCH     "AutoCredit of Florida charge" TO IDesc1
         GOTO      ItemACF1 IF EQUAL
         MATCH     "Special pricing for reports " TO IDesc1
         GOTO      ItemSpc3 IF EQUAL
         MATCH     "sent to all 3 credit bureaus" TO IDesc2    ;; **
         GOTO      ItemSpc3 IF EQUAL                           ;; **
         GOTO      PTIFOut

ItemCBI1
         MOVE      DC TO PTItemID
         MOVE      DCdC TO PTDescrp
         GOTO      PTIFOut
ItemTRW1
         MOVE      DT TO PTItemID
         MOVE      DCdT TO PTDescrp
         GOTO      PTIFOut
ItemTU1
         MOVE      DU TO PTItemID
         MOVE      DCdU TO PTDescrp
         GOTO      PTIFOut
. Special coding for Auto Credit of Florida
ItemACF1
         MOVE      DCFC TO PTItemID
         MOVE      DCdF TO PTDescrp
         GOTO      PTIFOut

. Special coding for Sound Ford
ItemSF1
         MOVE      DCSF  TO PTItemID
         MOVE      DCdSF TO PTDescrp
         GOTO      PTIFOut
ItemSpc3
         MOVE      DS TO PTItemID
         MOVE      DCdS TO PTDescrp
         GOTO      PTIFOut

PTIFOut
         WRITE     PTIF,PTIFSeq;*LL,PTImport

         MOVE      ICust TO SaveCust

. *************************************************************************
         KEYIN     *R,*N,*C,"At PTIFOut. Finished Inv Prt.":
                   *-,Reply;
         GOTO   EOJ
. *************************************************************************


         GOTO      ReadTime

. Called routine to write the Invoice Billing Month line since we are
. usually way behind in invoicing. The 'Billing for Professional Services'
. will be the month we did the work.

BMLine
         CLEAR     PTItemID                   ;; No Item ID
         MOVE      ARAcct   TO PTARAcct       ;; A/R Account Number
         CLEAR     PTInvNbr                   ;; 1/12/98 Null field
         MOVE      CustNum  TO PTCustID       ;; Customer ID
         MOVE      IPDate   TO PTDate         ;; Date
         MOVE      "0"      TO WkAmount       ;; Amount (Total)
         MOVE      WkAmount TO PTAmount       ;; Back to output area
         REPLACE   " 0"     IN PTAmount       ;; Zero fill
         CMOVE     "-"      TO PTAmount       ;;   Debit

         PACK      PTJobID FROM BMonth, MTYear, "DC"
         MOVE      "0"      TO WkUnitPr       ;; Unit price
         MOVE      WkUnitPr TO PTUnitPr       ;; Unit price
         REPLACE   " 0"     IN PTUnitPr       ;; Zero fill
         MOVE      "0"      TO WkQuant        ;; Quantity
         MOVE      WkQuant  TO PTQuant        ;; Quantity
         REPLACE   " 0"     IN PTQuant        ;; Zero fill
         MOVE      Exempt TO PTTaxTyp         ;; Non-Taxable line item
         PACK      PTDescrp FROM InvLine1, " ", BillMnth
         MOVE      DCAcct TO PTGLAcct         ;; GL Code for DC

         WRITE     PTIF,PTIFSeq;*LL,PTImport
         RETURN


TotalInv
.
. ============================================================================
.
.  Sales Tax Processing                (3/31/88)
.
         MOVE      "0.00"   TO IFTAXAMT   /* Clear record tax amt field
         COMPARE   "0.00"   TO ACCUMST    /* Any Sales Tax to print?
         GOTO       NOSTAX IF EQUAL       /* No,  just skip the rest
         MULTIPLY  TAXRATE  BY ACCUMST    /* Get tax amount
         MOVE      ACCUMST  TO SALESTAX   /* Move to work field
         MOVE      EAMTMASK TO DSTAX      /* Move to print field
         EDIT      SALESTAX TO DSTAX      /* Edit for printing...
         MOVE      SALESTAX TO IFTAXAMT   /* Move to File output area
         ADD       SALESTAX TO INVTOTAL   /* Add  to Invoice Total

         PRINT     *N:
                   *55,"Sales Tax":
                   *66,DSTAX

NoSTax
.
. ============================================================================
.
         CALL      AddIF                         /* Add inv to history file
         DISPLAY   *P76:11,*COLOR 19,SAVEINUM:   /* Display this invoice number
                   *COLOR 23;
.
         MOVE      TAMTMASK TO DITOTAL
         EDIT      INVTOTAL TO DITOTAL
.
         PRINT     *N:
                   *66,"----------":
                   *N:
                   *N:
                   *43,"Total Amount Due":
                   *64,DITOTAL:
                   *N:
                   *66,"==========":        /* 7/27...JMM
                   *N,*F                    /* 7/27...JMM

         ADD       "1" TO NBRINVS           /* NUMBER OF INVOICES PRINTED
         ADD       INVTOTAL TO GRANDTOT     /* ACCUMULATE INVOICE TOTALS
         MOVE      "0.0" TO INVTOTAL        /* CLEAR INVOICE TOTAL
         MOVE      "0.0" TO ACCUMST         /* Clear Sales Tax Total
         ADD       "1" TO SAVEINUM
         CALL      GetDist                  ;; Routine to get distribution
         MOVE      DITOTAL TO PSITOTAL      /* Save for Summary report

         CMATCH    "N" TO EOJSW
         GOTO      READAR IF EQUAL
         GOTO      EOJEXIT

AddIF
         MOVE      CUSTNUM TO IFCUSTID      /* CUSTOMER NUMBER
         MOVE      SAVEINUM TO IFINV        /* INVOICE NUMBER
         MOVE      SAVEINUM TO INVKEY       /* INVOICE NUMBER FOR KEYED READ
         MOVE      SAVEIWO  TO IFWO         /* Work Order number
         CLEAR     IFKEY
         APPEND    IFCUSTID TO IFKEY
         APPEND    IFINV TO IFKEY
         RESET     IFKEY
         MOVE      INVTOTAL TO IFIAMT$    /* INVOICE TOTAL AMOUNT
         PACK      IFIDATE FROM BILLMO, DAY, YEAR
         MOVE      BLANKS TO IFPAIDD      /* CLEAR DATE PAID
.
. Delete the key for this invoice number if it has already been used
.
         TRAP      ADDIF2 IF RANGE
         READ      INVOICES,INVKEY;IFSCUST,IFSINV
         GOTO      DUPEIR IF NOT OVER
         GOTO      ADDIF3
.
ADDIF2
         NORETURN
ADDIF3
         TRAPCLR   RANGE
         MOVE      IFKEY TO IFKEYSV         /* Save the potential new key
         TRAP      DUPEIR2 IF IO
         CALL      WRITEIF
         INSERT    INVOICES,INVKEY
         TRAPCLR   IO
         RETURN
.
.
DUPEIR
         MOVE      IFKEY TO IFKEYSV
         CLEAR     IFKEY
         APPEND    IFSCUST TO IFKEY
         APPEND    IFSINV TO IFKEY
         RESET     IFKEY
DUPEIR2
         READ      TJONINVF,IFKEY;REPLY
         DELETE    TJONINVF,IFKEY
         DELETEK   INVOICES,IFSINV
         MOVE      IFKEYSV TO IFKEY
         CALL      WRITEIF
         MOVE      IFINV TO IFSINV
         INSERT    INVOICES,IFSINV
         RETURN
WriteIF
         WRITE     TJONINVF,IFKEY;*-,IFRecord
         RETURN                       /* Return to calling location

.
.    EOJ processing
.
EOJExit
.
.    Print the grand total sheet
.
         MOVE      TAMTMASK TO DITOTAL
         EDIT      GRANDTOT TO DITOTAL

         DISPLAY   *COLOR 23,*P1:22,*EL,*P1:23,*EL,*P1:24,*EL:
                   *P10:22,InvLine1," ",BillMnth:
                   *P10:23,"Total number of invoices printed----------> ":
                   *COLOR 19,NBRINVS:
                   *COLOR 23:
                   *P10:24,"Total amount of invoices printed----------> ":
                   *COLOR 19,DITOTAL:
                   *COLOR 23;
         KEYIN     *CL,Reply;

         PRINT     *N,*N,*N,*N:
                   *N,*N,*N,*N:
                   *N,*N,*N,*N:
                   "        ":
                   INVLINE1," ",BILLMNTH:
                   *N:
                   "     ":
                   "   Total number of invoices printed---------->          ":
                   NBRINVS:
                   *N:
                   "     ":
                   "   Total amount of invoices printed----------> ",DITOTAL:
                   *N,*F
         CMATCH    "Y" TO INTSW
         GOTO      NOREL IF NOT EQUAL
         PRINT     *N,*N,*N,"  Printing has been interrupted....",*N,*F
NOREL
. *************************************************************************
         KEYIN     *R,*N,*C,"At NoRel Closing SplFile=":
                   *DV,SplName,"|",*-,Reply;
. *************************************************************************
. *************************************************************************
         PRINT     SplFile;"       Print following NoRel. Explicit":
                           " specification of SplFile "
         PRINT     "Second Print following NoRel. No":
                           " specification of SplFile "
. *************************************************************************
         SPLCLOSE  SplFile                         /* 1/21/99...JMM
         CLOSE     CUSTMAST
. ==================================== PeachTree Mods   Jan 98
.
         WEOF      PTIF,PTIFSeq             ;; 1/11/98 Peachtree mods
         CLOSE     PTIF                     ;; 1/10/98 Peachtree Imp File
.
. ==================================== PeachTree Mods   Jan 98
.
. CODE ADDED 10/9/78...JMM TO KILL TIMEFILE
.
         KEYIN     *COLOR 23,*P01:20,*EL,*B:
                   "Do you want to DELETE 'TIMEFILE' and update ":
                   "invoice number? (Y/N) ",*EL,REPLY;
         CMATCH    "Y" TO REPLY
         GOTO      KILLTF IF EQUAL
         CMATCH    "y" TO REPLY
         GOTO      KILLTF IF EQUAL
         GOTO      SHUTDN
KILLTF
         PREPARE   TIMEFILE,"TIMEFILE"         7/9/79...JMM
         CLOSE     TIMEFILE                    7/9/79...JMM
.
.   UPDATE THE NEXT INVOICE NUMBER
.
         TRAP      CTRLMISS IF IO
         OPEN      CTRLFILE,"CTRLFILE"
         TRAPCLR   IO
         TRAP      NOCTLREC IF RANGE
         READ      CTRLFILE,CTRLKEY;FILLER,NEXTINV
         GOTO      NOCTLREC IF OVER
         TRAPCLR   RANGE
         MOVE      SAVEINUM TO NEXTINV
         UPDATE    CTRLFILE;FILLER,NEXTINV

.   Update the Last Billing Month detail record

         MOVE      "LAST BILL MO" TO CTRLKEY
         READ      CTRLFILE,CTRLKEY;CTRLKEY,Reply:
                                    LastBMM,Reply:
                                    LastBYY,Reply
         GOTO      NOCTLREC IF OVER

. Bump to next Month (and Year if last was December)

         ADD       "1" TO LastBMM
         COMPARE   "13" TO LastBMM
         GOTO      NotDec IF NOT EQUAL
         MOVE      "01" TO LastBMM
         ADD       "1" TO LastBYY
NotDec
         MOVE      LastBMM TO MMWork
         REPLACE   " 0" IN MMWork
         MOVE      LastBYY TO YYWORK
         REPLACE   " 0" IN YYWork
         MOVE      NextInv TO LastEIN
         MOVE      GrandTot TO LastAmt
         UPDATE    CTRLFILE;CTRLKEY,Reply:
                            MMWork,Reply:
                            YYWork,Reply:
                            LastSIN,Reply:
                            LastEIN,Reply:
                            LastAmt

         CLOSE     CTRLFILE
         CLOSE     TIMEFILE                     7/9/79...JMM
.
ShutDn
         MATCH     "Y" TO CmdLine
         GOTO      NoChain IF EQUAL       ;; Do we enter via EXECUTE?
         CHAIN     "TJONMENU"             GO BACK TO MENU    12/5...JMM
NoChain
         STOP
INT
         NORETURN
         CMOVE     "Y" TO IntSw
         GOTO      EOJExit

. This was implemented as a called routine on 1/10/98
CkPTIF
         TRAP      NewPTIF IF IO
         OPEN      PTIF,PTIFName
         CLOSE     PTIF
         TRAPCLR   IO
         KEYIN     *COLOR 124,*B,*P1:24,*EL:
                   "The Peachtree import file ",*DV,PTIFName:
                   " already exists! ":
                   *P1:25,"Write over? (Y/N) ":
                   *CL,*-,*UC,Reply,*LC,*COLOR 23;
         CMATCH    "Y" TO Reply
         GOTO      NewPTIF2 IF EQUAL
         KEYIN     *COLOR 124,*P01:25,*EL,"Append to the end? (Y/N) ":
                   *CL,*-,*UC,REPLY,*LC,*COLOR 23;
         CMATCH    "Y" TO Reply
         GOTO      CkPTIF IF NOT EQUAL ;; They have to do one or the other
         POSITEOF  PTIF              ;; New records will be appended to EOF
         GOTO      CkIFExit          ;; and exit the Import File Start Routine
NewPTIF
         NORETURN
NewPTIF2
         TRAPCLR   IO
         MOVE      Forward TO PTIFSeq    ;; Sequential file processing (forward)
         PREPARE   PTIF,PTIFName
         WRITE     PTIF,PTIFSeq;PTHeader ;; Start with Header record
CkIFExit
         RETURN

. Called routine to read INVDIST and get the number of distributions for
.    the corresponding invoice number. This file was created/updated in
.    program INVOICE.DBS
GetDist
         MOVE      SaveINum TO IDKey
         READ      InvDist,IDKey;IDKey,IDNbr
         GOTO      GDExit IF NOT OVER
         CMATCH    "N" TO EOJSW
         GOTO      GDExit IF NOT EQUAL
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice missing in INVDIST file. IDKey=":
                   *DV,SaveINum:
                   REPLY,*COLOR 23;
GDExit
         MOVE      IDNbr   TO WkDistr          ;; 1/11/98
         ADD       "1"     TO WkDistr          ;; 1/21/98 Bump for BMLine
         MOVE      WkDistr TO PTDistr          ;; 1/11/98
         REPLACE   " 0"    IN PTDistr          ;; Zero fill
         RETURN

. This was changed to a called routine on 7/22/95
CkSF
         TRAP      NewSF IF IO
         MOVE      "TJONINV3.PRT" TO SFName
         PACK      SplName FROM Drive, SFName, Blanks
         OPEN      SF,SplName
         CLOSE     SF
         TRAPCLR   IO
         KEYIN     *COLOR 124,*B,*P1:24,*EL:
                   "The print file ",*DV,SFName," already exists! ":
                   " Write over? (Y/N) ",*CL,*-,*UC,REPLY,*LC,*COLOR 23;
         CMATCH    "Y" TO REPLY
         GOTO      NEWSF2 IF EQUAL
         KEYIN     *COLOR 124,*P52:24,*EL,"Append to the end? (Y/N) ":
                   *CL,*-,*UC,REPLY,*LC,*COLOR 23;
         CMATCH    "Y" TO REPLY
         GOTO      SHUTDN IF NOT EQUAL
. *************************************************************************
         KEYIN     *R,*N,*C,"At CkSF Appending to SplFile=":
                   *DV,SplName,"|",*-,Reply;
. *************************************************************************
         SPLOPEN   SplFile,SplName,"Q" /* APPEND TO THE CURRENT SPOOLFILE
         GOTO      CKSFEXIT           /* AND EXIT THE SPOOL FILE START RTN
NewSF
         NORETURN
NewSF2
         TRAPCLR   IO
. 1/22/99 Force output to $TDSDATA (See PLBENV_TDSDATA in PLBWin.INI)
         MOVE      "TJONINV3.PRT" TO SFName    /*
         PACK      SplName FROM Drive, SFName, Blanks   ;; 1/22/99
. *************************************************************************
         KEYIN     *R,*N,*C,"At NewSF2 Creating new SplFile=":
                   *DV,SplName,"|",*-,Reply;
. *************************************************************************

         SPLOPEN   SplFile,SplName
. *************************************************************************
         PRINT     SplFile;"First  Print following SPLOPEN. Explicit":
                           " specification of SplFile "
         PRINT     "Second Print following SPLOPEN. No":
                           " specification of SplFile "
. *************************************************************************
CKSFEXIT
. No-Op  PRINT     *F," "; ,LSTJECL            /* 7/27/87.... Changed...JMM
. No-Op  GOTO      READTIME                    /* 3/31/88...JMM
         RETURN                                /* 7/22/95 Changed to called rtn

InvMiss
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice Master file is missing (TJONINVF) ":
                   REPLY,*COLOR 23;
         STOP

InvDMiss
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice Distribution file is ":
                   "missing (INVDIST) ":
                   Reply,*COLOR 23;
         STOP

ARMiss
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Customer Master File is missing. ":
                   "(CUSTMAST) ":
                   REPLY,*COLOR 23;
         STOP

NoInvRec
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"No Invoice Record found.....":
                   REPLY,*COLOR 23;
         STOP

NoTime
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"No Time Accounting Record found...":
                   REPLY,*COLOR 23;
         STOP

CtrlMiss
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice Control file is missing (CTRLFILE) ":
                   REPLY,*COLOR 23;
         STOP

CustNF
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Customer ",*DV,Key:
                   " is not in CUSTMAST file. ":
                   REPLY,*COLOR 23;
         STOP

NoIF
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice History File is ":
                   "missing  (TJONINVF.ISI or TJONINVF.TXT) ":
                   REPLY,*COLOR 23;
         STOP
.
NoIF2
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Secondary Invoice History File Index is ":
                   "missing  (INVOICES.ISI or TJONINVF.TXT) ":
                   REPLY,*COLOR 23;
         STOP

NoCtlRec
         KEYIN     *COLOR 124:
                   *B,*P1:24,*EL,"Invoice control record is missing ":
                   "(CTRLFILE) ":
                   REPLY,*COLOR 23;
         STOP

EOJ      MOVE      "Y" TO EOJSW
         TRAPCLR   RANGE
         GOTO      TOTALINV
